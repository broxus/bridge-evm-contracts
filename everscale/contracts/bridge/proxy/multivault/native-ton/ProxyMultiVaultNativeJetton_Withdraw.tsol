pragma ever-solidity >= 0.62.0;

import "../../../interfaces/event-contracts/IEverscaleEthereumEvent.tsol";
import "../../../interfaces/event-configuration-contracts/IEverscaleEthereumEventConfiguration.tsol";

import "../../../../utils/jetton/interfaces/ITransferNotification.tsol";

import "./ProxyMultiVaultNativeJetton_Base.tsol";

abstract contract ProxyMultiVaultNativeJetton_Withdraw is
    ProxyMultiVaultNativeJetton_Base,
    ITransferNotification
{
    /// @notice Handles incoming token transfer
    /// Initializes native token withdraw
    /// @param _amount Tokens amount
    function transferNotification(
        uint64 /*_callId*/,
        varUint16 _amount,
        address _tokenSender,
        optional(TvmCell) _payload
    ) external override functionID(0x7362d09c) reserveAtLeastTargetBalance {
        address remainingGasTo = _tokenSender;

        (
            uint32 nonce,
            Network network,
            TvmCell transferPayload,
            string name,
            string symbol,
            uint8 decimals,
            address tokenRoot
        ) = abi.decode(_payload.get(), (
            uint32,
            Network,
            TvmCell,
            string,
            string,
            uint8,
            address
        ));

        if (network == Network.EVM) {
            (
                uint160 recipient,
                uint256 chainId,
                EVMCallback callback
            ) = abi.decode(transferPayload, (
                uint160,
                uint256,
                EVMCallback
            ));

            _deployEVMEvent(
                nonce,
                tokenRoot,
                chainId,
                _amount,
                recipient,
                remainingGasTo,
                _tokenSender,
                callback,
                name,
                symbol,
                decimals
            );
        }
    }

    function _deployEVMEvent(
        uint32 nonce,
        address token,
        uint256 chainId,
        uint128 amount,
        uint160 recipient,
        address remainingGasTo,
        address sender,
        EVMCallback callback,
        string name,
        string symbol,
        uint8 decimals
    ) internal view {
        TvmCell eventData = abi.encode(
            nonce,
            address(this), // Proxy address
            msg.sender, // Token wallet address, must be validated in the event contract
            token, // Token root
            remainingGasTo, // Remaining gas to
            amount, // Amount of tokens to withdraw
            recipient, // EVM recipient address
            chainId, // EVM network chain ID
            sender,
            callback.recipient,
            callback.payload,
            callback.strict,
            name,
            symbol,
            decimals
        );

        IEverscaleEthereumEvent.EverscaleEthereumEventVoteData eventVoteData = IEverscaleEthereumEvent.EverscaleEthereumEventVoteData(
            tx.timestamp,
            now,
            eventData
        );

        IEverscaleEthereumEventConfiguration(evmConfiguration.everscaleConfiguration)
            .deployEvent{
                value: 0,
                bounce: false,
                flag: MsgFlag.ALL_NOT_RESERVED
            }(eventVoteData);
    }
}
