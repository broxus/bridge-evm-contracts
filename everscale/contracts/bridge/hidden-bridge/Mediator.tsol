pragma ever-solidity >= 0.39.0;

pragma AbiHeader expire;

import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "@broxus/contracts/contracts/access/InternalOwner.tsol";
import "@broxus/contracts/contracts/utils/RandomNonce.tsol";

import "./../../utils/TransferUtils.tsol";

import "ton-eth-bridge-token-contracts/contracts/interfaces/IAcceptTokensTransferCallback.tsol";
import "ton-eth-bridge-token-contracts/contracts/interfaces/IAcceptTokensMintCallback.tsol";
import "ton-eth-bridge-token-contracts/contracts/interfaces/IBurnableTokenWallet.tsol";
import "ton-eth-bridge-token-contracts/contracts/interfaces/ITokenWallet.tsol";
import "ton-eth-bridge-token-contracts/contracts/TokenWalletPlatform.tsol";


/// @title Mediator
/// @notice Used as part of hidden bridge for evm-ever-evm token transfers
/// An example:
/// 1. ETH USDT -> EVER USDT -> BNB USDT
/// 1.1 Mediator accepts EVER USDT mint and decodes the payload
/// 1.2 Payload says "Burn tokens in favour of proxy with some payload"
/// 1.3 Burn initiates creating an ever-evm event contract with BNB USDT withdrawal
contract Mediator is IAcceptTokensTransferCallback, IAcceptTokensMintCallback, TransferUtils, InternalOwner, RandomNonce {
    enum Operation { Burn, Transfer }

    uint128 constant DEPLOY_WALLET_VALUE = 0.2 ever;

    TvmCell alienTokenWalletPlatformCode;

    constructor(
        address _owner,
        TvmCell _alienTokenWalletPlatformCode
    ) public {
        tvm.accept();

        setOwnership(_owner);
        alienTokenWalletPlatformCode = _alienTokenWalletPlatformCode;
    }

    /// @notice Accept incoming mint
    function onAcceptTokensMint(
        address tokenRoot,
        uint128 amount,
        address remainingGasTo,
        TvmCell payload
    ) external override reserveAtLeastTargetBalance {
        (
            Operation operation,
            address proxy,
            TvmCell operationPayload
        ) = abi.decode(payload, (Operation, address, TvmCell));

        address wallet = _deriveAlienTokenWallet(tokenRoot);

        if (operation == Operation.Burn) {
            _burn(wallet, amount, proxy, remainingGasTo, operationPayload);
        } else if (operation == Operation.Transfer) {
            _transfer(wallet, amount, proxy, remainingGasTo, operationPayload);
        }
    }

    /// @notice Accept incoming transfer
    function onAcceptTokensTransfer(
        address,
        uint128 amount,
        address,
        address,
        address remainingGasTo,
        TvmCell payload
    ) external override reserveAtLeastTargetBalance {
        (
            Operation operation,
            address proxy,
            TvmCell operationPayload
        ) = abi.decode(payload, (Operation, address, TvmCell));

        address wallet = msg.sender;

        if (operation == Operation.Burn) {
            _burn(wallet, amount, proxy, remainingGasTo, operationPayload);
        } else if (operation == Operation.Transfer) {
            _transfer(wallet, amount, proxy, remainingGasTo, operationPayload);
        }
    }

    function _transfer(
        address wallet,
        uint128 amount,
        address proxy,
        address remainingGasTo,
        TvmCell payload
    ) internal pure {
        ITokenWallet(wallet).transfer{
            bounce: false,
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED
        }(amount, proxy, DEPLOY_WALLET_VALUE, remainingGasTo, true, payload);
    }

    function _burn(
        address wallet,
        uint128 amount,
        address proxy,
        address remainingGasTo,
        TvmCell payload
    ) internal pure {
        IBurnableTokenWallet(wallet).burn{
            bounce: false,
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED
        }(amount, remainingGasTo, proxy, payload);
    }

    function _deriveAlienTokenWallet(
        address root
    ) internal view returns(address) {
        TvmCell stateInit = tvm.buildStateInit({
            contr: TokenWalletPlatform,
            varInit: {
                root: root,
                owner: address(this)
            },
            pubkey: 0,
            code: alienTokenWalletPlatformCode
        });

        return address(tvm.hash(stateInit));
    }

    function upgrade(
        TvmCell code
    ) external onlyOwner {
        TvmCell data = abi.encode(
            owner,
            alienTokenWalletPlatformCode,
            _randomNonce
        );

        tvm.setcode(code);
        tvm.setCurrentCode(code);

        onCodeUpgrade(data);
    }

    function onCodeUpgrade(
        TvmCell data
    ) private {
        tvm.resetStorage();

        (
            owner,
            alienTokenWalletPlatformCode,
            _randomNonce
        ) = abi.decode(data, (address, TvmCell, uint));
    }
}
